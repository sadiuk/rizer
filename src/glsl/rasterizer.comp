#version 450 core
#define VERTEX_BUFFER_LAYOUT %d

struct vertex
{
#if VERTEX_BUFFER_LAYOUT == 1
	vec3 pos;
#elif VERTEX_BUFFER_LAYOUT == 2
	vec3 pos;
	vec4 color;
#endif
};

layout(local_size_x = 16, local_size_y = 16) in;

layout(std140, binding = 0) uniform RasterParams
{
	mat4 proj;	
	mat4 view;
	mat4 model;
	uvec2 texSize;
	vec4 color;
	vec3 clear_color;
	bool enable_depth_testing;
	bool update_depth_buffer;
} raster_params;

layout(binding = 0, rgba8) uniform image2D out_tex;
layout(binding = 1, std430) buffer VertexBuffer
{
	vertex vertices[];
};
layout(binding = 2, std430) buffer IndexBuffer
{
	uvec3 indices[];
};
layout(binding = 3, std430) buffer DepthBuffer
{
	float depthBuffer[];
};

struct PointTriangleOrientation
{
	vec3 edge_function_results;
	vec3 barycentrics;
	float current_z;
};

void TestPointInTriangle(in vec3 p1, in vec3 p2, in vec3 p3, vec2 test_point, out PointTriangleOrientation results);
bool PerformDepthTest(uvec2 int_coord, float depth, bool updateDepthBuffer);
void UpdateOutColor(in uint triangle_index, out vec4 color);
void main()
{
	if(all(lessThan(gl_GlobalInvocationID.xy, raster_params.texSize)))
	{
		vec2 texelCoord = (gl_GlobalInvocationID.xy / vec2(raster_params.texSize) - 0.5) * 2; 
		uint buffer_coord = gl_GlobalInvocationID.y * raster_params.texSize.x + gl_GlobalInvocationID.x;
		depthBuffer[buffer_coord] = -1000;
	
		PointTriangleOrientation orientation;

		uint indices_count = indices.length();
		bool pixel_is_covered = false;
		vec4 out_color = raster_params.color;
		for(uint i = 0; i < indices_count; i++)
		{
			uvec3 triangle_indices = indices[i];
			vec4 p1_before_perspective = (raster_params.proj * raster_params.view * raster_params.model * vec4(vertices[triangle_indices[0]].pos, 1));
			vec4 p2_before_perspective = (raster_params.proj * raster_params.view * raster_params.model * vec4(vertices[triangle_indices[1]].pos, 1));
			vec4 p3_before_perspective = (raster_params.proj * raster_params.view * raster_params.model * vec4(vertices[triangle_indices[2]].pos, 1));
			vec3 p1 = p1_before_perspective.xyz / p1_before_perspective.w;
			vec3 p2 = p2_before_perspective.xyz / p2_before_perspective.w;
			vec3 p3 = p3_before_perspective.xyz / p3_before_perspective.w;
			if(p1_before_perspective.w < -0.01 || p2_before_perspective.w < -0.01 || p3_before_perspective.w < -0.01) break;
			//vec3 p1 = vertices[triangle_indices[0]].pos;
			//vec3 p2 = vertices[triangle_indices[1]].pos;
			//vec3 p3 = vertices[triangle_indices[2]].pos;
			TestPointInTriangle(p1, p2, p3, texelCoord, orientation);

			if(all(lessThanEqual(orientation.barycentrics, vec3(1))) && all(greaterThanEqual(orientation.barycentrics, vec3(0))))
			{
				pixel_is_covered = true;
				if(raster_params.enable_depth_testing)
				{
					if(PerformDepthTest(gl_GlobalInvocationID.xy, orientation.current_z, raster_params.update_depth_buffer))
					{
						vec4 c1, c2, c3;
						UpdateOutColor(triangle_indices[0], c1);
						UpdateOutColor(triangle_indices[1], c2);
						UpdateOutColor(triangle_indices[2], c3);
						vec3 er = orientation.barycentrics;
						out_color = vec4((c1 * er.x + c2 * er.y + c3 * er.z).xyz, 1);
					}
				}
			}
		}
		if(!pixel_is_covered)
		{
			imageStore(out_tex, ivec2(gl_GlobalInvocationID.xy), vec4(raster_params.clear_color, 1));
		}
		else 
		{
			imageStore(out_tex, ivec2(gl_GlobalInvocationID.xy), out_color);
		}
	}
}

void UpdateOutColor(in uint triangle_index, out vec4 color)
{
#if VERTEX_BUFFER_LAYOUT == 1
	color = raster_params.color;
#elif VERTEX_BUFFER_LAYOUT == 2
	color = vertices[triangle_index].color;
#endif
}
bool PerformDepthTest(uvec2 int_coord, float depth, bool update_depth_buffer)
{
	uint buffer_coord = int_coord.y * raster_params.texSize.x + int_coord.x;
	float current_depth = depthBuffer[buffer_coord];
	bool depth_test_passed = false;
	if(depth > current_depth)
	{
		depth_test_passed = true;
	}
	if(depth_test_passed && update_depth_buffer)
	{
		depthBuffer[buffer_coord] = depth;
	}
	return depth_test_passed;
}

float EdgeFunction(in vec2 p1, in vec2 p2, in vec2 test_point)
{
	return (test_point.x - p1.x) * (p2.y - p1.y) - (test_point.y - p1.y) * (p2.x - p1.x);
}

void TestPointInTriangle(in vec3 p1, in vec3 p2, in vec3 p3, vec2 test_point, out PointTriangleOrientation results)
{
	results.edge_function_results = vec3(EdgeFunction(p2.xy, p3.xy, test_point),
										 EdgeFunction(p3.xy, p1.xy, test_point),
										 EdgeFunction(p1.xy, p2.xy, test_point));
	vec3 triangle_area = vec3(EdgeFunction(p1.xy, p2.xy, p3.xy));
	results.barycentrics = (results.edge_function_results / triangle_area);
	vec3 b = results.barycentrics;
	results.current_z = 1 / p1.z * b[0] + 1 / p2.z * b[1] + 1 / p3.z * b[2]; 

}